plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
	id "io.freefair.lombok" version "3.8.1"
	id 'net.ltgt.apt' version '0.20'
	id 'com.google.cloud.tools.jib' version '1.4.0'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt-idea'

group = 'com.epam'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	mapstructVersion = "1.3.0.Final"
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

compileJava {
	options.compilerArgs = [
			'-Amapstruct.defaultComponentModel=spring'
	]
}

repositories {
	mavenCentral()
}

sourceSets {
	e2e {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	e2eImplementation.extendsFrom implementation
	e2eRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	e2eImplementation 'junit:junit:4.12'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compile 'io.springfox:springfox-swagger2:2.9.2'
	compile 'io.springfox:springfox-swagger-ui:2.9.2'
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

task e2eTest(type: Exec) {
	description = 'Runs end-to-end tests.'
	group = 'verification'
    commandLine 'bzt', 'src/e2e/taurus/end-to-end.yml'
}

check.dependsOn e2eTest